plugins {
    id 'dev.architectury.loom' version '0.7.2-SNAPSHOT'
    id "com.github.johnrengelman.shadow" version "5.0.0"
}


sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

configurations {
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
}

loom {
    mixinConfigs = ["cinderscapes.mixins.json"]
    useFabricMixin = true
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"

    forge "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    def wood = "com.terraformersmc.terraform-api:terraform-wood-api-v1:${project.terraform_wood_api_version}"
    modImplementation wood
    shadowCommon wood

    def shapes = "com.terraformersmc.terraform-api:terraform-shapes-api-v1:${project.terraform_shapes_api_version}"
    modImplementation shapes
    shadowCommon shapes
    modCompileOnly "com.terraformersmc:vistas:${project.vistas_version}"
}

repositories {
    maven {
        url "https://maven.shedaniel.me"
        content {
            includeGroup "me.shedaniel.cloth.api"
            includeGroup "me.sargunvohra.mcmods"
            includeGroup "me.shedaniel.cloth"
            includeGroup "me.shedaniel"
        }
    }
    maven {
        name = 'TerraformersMC'
        url = 'https://maven.terraformersmc.com/'
    }
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}


// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

shadowJar {
    configurations = [project.configurations.shadowCommon]
    classifier "dev-shadow"
}


remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    classifier ""
}

jar {
    classifier "dev"
}
